/JOB
//NAME R1_ALNPICK_TRAY
//POS
///NPOS 1,0,0,5,0,0
///USER 9
///TOOL 2
///POSTYPE USER
///RECTAN
///RCONF 1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0
C00000=138.121,-286.431,-28.057,-179.9997,0.0029,0.0008
P00115=122.268,-311.055,-17.000,180.0000,0.0000,0.0000
P00116=150.011,150.021,-83.000,-179.9991,-0.0013,0.0038
P00118=148.983,148.978,-43.327,-179.9990,0.0016,0.0114
P00119=131.311,129.617,45.233,179.9975,-10.7250,-0.0042
///POSTYPE BASE
P00124=327.058,1091.445,1024.191,179.2677,1.3067,89.0784
//INST
///DATE 2022/07/25 14:31
///ATTR SC,RW,RJ
////FRAME USER 9
///GROUP1 RB1
///LVARS 0,0,10,0,0,10,0,0
NOP
'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
' Opportunity job template
'%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
' 
*TOP
'***********INITIALIZE***********
' Set Clear of Module Bits
CALL JOB:R1_CLEARMODULE ARGF244
' Report Position
CALL JOB:R1_REPORTPOS ARGF12
'Request Enter
CALL JOB:R1_REQUESTENTER
'Tool Check
CALL JOB:R1_TOOLCHECK ARGFI011 ARGF2000 ARGF2000
'********************************
' 
'********PRE-STATUS_CHECK********
'Verify alignment
'~~~ Calc place pos
'Tray L
SET LD000 D017
'Tray W
SET LD001 D018
'~~~Update approach/pick
'Pick_ALN_Tray
SET LP000 P116
'APP_ALN_Tray
SET LP001 P118
'Conversion math
CNVRT LPX000 LPX000 UF#(9) TL#(2)
CNVRT LPX001 LPX001 UF#(9) TL#(2)
SET LD000  EXPRESS LD000 / 2
SET LD001  EXPRESS LD001 / 2
SETE LP000 (1) LD000
SETE LP000 (2) LD001
SETE LP001 (1) LD000
SETE LP001 (2) LD001
'********************************
' 
'***********PRIME_MOVE***********
'Move to Prework Position
'Wait for ALN Grip
WAIT IN#(492)=ON
MOVJ P119 VJ=50.00 PL=2 ACC=20 DEC=20
'Move to Approach Position
MOVJ LP001 VJ=50.00 PL=2 ACC=20 DEC=20
'Set Shock Sensitivity
SHCKSET RB1 SSL#(2)
'Move to pick position
MOVL LP000 V=300.0 ACC=20 DEC=20
'Activate vacuum
CALL JOB:R1_VACPICK
DOUT OT#(316) ON
WAIT IN#(493)=ON
DOUT OT#(316) OFF
PULSE OT#(317) T=1.00
'Move to Retract
MOVJ P119 VJ=25.00 PL=2 ACC=20 DEC=20
MOVL C00000 V=300.0 PL=2 ACC=20 DEC=20
'Reset Shock Sensitivity
SHCKRST RB1
'Move to Prework Position
MOVJ P115 VJ=25.00 PL=2 ACC=20 DEC=20
'Move to Trans Point
MOVJ P124 VJ=25.00 PL=2 ACC=20 DEC=20
'********************************
' 
'*******POST-STATUS_CHECK********
'********************************
' 
'****USER_DEFINED_PARAMETERS*****
'********************************
' 
'********DATA_VALIDATION*********
'********************************
' 
'*************SETUP**************
'********************************
' 
'*************ACTION*************
'********************************
' 
'*********HOUSE_KEEPING**********
'Clear Module
CALL JOB:R1_CLEARMODULE ARGF244
'Housekeeping
SET B004 1
'********************************
*OUT
END
